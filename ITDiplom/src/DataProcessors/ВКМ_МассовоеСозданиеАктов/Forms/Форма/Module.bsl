
#Область ОбработчикиСобытийФормы

//Добавление процедуры
//Макаров А.С.
//04.05.2024

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Месяц = Формат(Объект.Период, "ДФ='MMMM yyyy'");
	Если Год(Объект.Период) = 1 Тогда
		СформироватьСписокВыбораМесяца(Год(ОбщегоНазначенияКлиент.ДатаСеанса()));
	Иначе
		СформироватьСписокВыбораМесяца(Год(Объект.Период));
	КонецЕсли;
	
КонецПроцедуры


//КонецРедактирования
//Макаров А.С.
//04.05.2024

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

//Добавление процедуры
//Макаров А.С.
//04.05.2024

&НаКлиенте
Процедура МесяцПриИзменении(Элемент)
	
	Если Месяц <> "" Тогда
		Если СтрДлина(Месяц) = 4 Тогда
			СформироватьСписокВыбораМесяца(Число(Месяц));
			Объект.Период = НачалоМесяца(Дата(Число(Месяц), 1, 1));
			Месяц = Формат(Объект.Период, "ДФ='MMMM yyyy'");
		Иначе
			НомМесяца = (Найти("янвфевмарапрмайиюниюлавгсеноктноядек", Нрег(Лев(Месяц, 3))) + 2) / 3; 
			ВыбрГод = Число(Прав(Месяц, 4));
			Объект.Период = НачалоМесяца(Дата(ВыбрГод, НомМесяца, 1));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//КонецРедактирования
//Макаров А.С.
//04.05.2024

#КонецОбласти

#Область ОбработчикиКомандФормы

//Добавление процедуры
//Макаров А.С.
//04.05.2024

&НаКлиенте
Процедура Создать(Команда)
	
	ДлительнаяОперация = СоздатьАктыНаСервере();
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = "Формирование актов...";
	Оповещение = Новый ОписаниеОповещения("ОперацияВФонеЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);  
	
КонецПроцедуры

//КонецРедактирования
//Макаров А.С.
//04.05.2024

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//Добавление процедуры
//Макаров А.С.
//04.05.2024

&НаКлиенте
Процедура СформироватьСписокВыбораМесяца(Год)

	Элементы.Месяц.СписокВыбора.Очистить();
	Элементы.Месяц.СписокВыбора.Добавить(Формат(Год - 1, "ЧГ=0"));
	Для к = 1 По 12 Цикл
		СформДата = Дата(Год, к, 1);
		Наим = Формат(СформДата, "ДФ = ММММ_гггг");
		Наим = СтрЗаменить(Наим, "_", " ");
		Элементы.Месяц.СписокВыбора.Добавить(Наим);
	КонецЦикла;
	Элементы.Месяц.СписокВыбора.Добавить(Формат(Год + 1, "ЧГ=0"));
	
КонецПроцедуры

//КонецРедактирования
//Макаров А.С.
//04.05.2024

//Добавление Функции
//Макаров А.С.
//04.05.2024

&НаСервере 
Функция СоздатьАктыНаСервере()
	
	
	ИмяФункции = "Обработки.ВКМ_МассовоеСозданиеАктов.СозданиеАктовРеализацииУслуг";
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Период", Объект.Период);
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = ИмяФункции;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", УникальныйИдентификатор); 
	Возврат ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, ИмяФункции,ПараметрыПроцедуры);

КонецФункции 

//КонецРедактирования
//Макаров А.С.
//04.05.2024

//Добавление процедуры
//Макаров А.С.
//04.05.2024

&НаКлиенте
Процедура ОперацияВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли; 
	
	СсылкаНаДокумент = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если Результат.Статус = "Выполнено" И СсылкаНаДокумент.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нет данных для создания документов. Возможно документы за данный период были созданы ранее";
		Сообщение.Сообщить();
	ИначеЕсли Результат.Статус = "Выполнено" И СсылкаНаДокумент.Количество() > 0 Тогда
		ДлительныеОперацииЗавершениеНаСервере(СсылкаНаДокумент);
		ОбщегоНазначенияКлиент.СообщитьПользователю("Формирование документов реализации выполнено успешно");
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Статус операции: Ошибка при выполнении операции");
	КонецЕсли;
	
КонецПроцедуры 

//КонецРедактирования
//Макаров А.С.
//04.05.2024

//Добавление процедуры
//Макаров А.С.
//04.05.2024
&НаСервере 
Процедура ДлительныеОперацииЗавершениеНаСервере(СсылкаНаДокумент)
	
	Объект.ТабличнаяЧасть.Очистить();

    Для Каждого Строка Из СсылкаНаДокумент Цикл
	НоваяСтрока = Объект.ТабличнаяЧасть.Добавить();
	НоваяСтрока.Договор = Строка.Договор;
	НоваяСтрока.Документ = Строка.Ссылка;
    КонецЦикла; 
	
КонецПроцедуры

//КонецРедактирования
//Макаров А.С.
//04.05.2024

#КонецОбласти
